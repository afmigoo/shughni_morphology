#############
### VERBS ###
#############

PATTERNS
(VerbNegation _morph_)? VerbBases GlobClitics?

PATTERN VerbBases
NPastVerbBase
PastVerbBase
ImperVerbBase
PerfVerbBase
InfVerbBase

PATTERN NPastVerbBase
LexiconVerbNpst     [<v><prs>:] (_morph_hyphen_ PresSuffixes)?
LexiconVerbNpst1sgSh [<v>:] [<prs><1sg>:м] # Карамшоев и БД

PATTERN PastVerbBase
LexiconVerbPstF     [<v><pst><f>:]
LexiconVerbPstFPl   [<v><pst><f_pl>:]
LexiconVerbPstM     [<v><pst><m>:]
# Past stems are equivalent to the present stem with an additional stop -t/-d (Parker 2023, p. 256)
LexiconVerbNpst     [<v>:] _morph_ [<pst>:{vДТ}]

PATTERN ImperVerbBase
LexiconVerbImper    [<v><imp>:]

PATTERN PerfVerbBase
LexiconVerbPerfF     [<v><prf><f>:]
LexiconVerbPerfM     [<v><prf><m>:]
LexiconVerbNpst     [<v>:] _morph_ [<prf>:{vҶЧ}]

PATTERN InfVerbBase
# Infinite stems are equivalent to the present stems with an additional stop -t/-d (Parker 2023, p. 256)
LexiconVerbNpst [<v>:] _morph_ [<inf>:{vДТ}] (_morph_hyphen_ PurpSuff)?

# Parker 8.2.1
LEXICON PresSuffixes
<1sg>:{Й}ум
<2sg>:{Й}и
<3sg>:{ДТ}
<1pl>:{Й}āм
<2pl>:{Й}ет
<3pl>:{Й}ен

LEXICON PurpSuff
<purp>:оw

PATTERN VerbNegation
VERBNEG _hyphen_

# Parker 8.2.2
LEXICON VERBNEG 
<neg>:на
<neg>:нā # internal gloss document
<neg>:мā


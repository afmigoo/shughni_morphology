#############
### VERBS ###
#############

PATTERNS
(VerbNegation _morph_)? VerbBases GlobClitics?

PATTERN VerbBases
NPastVerbBase
PastVerbBase
ImperVerbBase
PerfVerbBase
InfVerbBase
Participle2Base
ToBeParadigm

### Present ###
PATTERN NPastVerbBase
LexiconVerbNpst|LexiconVerbNpstSh       [<v><prs>:]
LexiconVerbNpst                         [<v>:] _morph_ [<prs>:] PresSuffixes  
LexiconVerbNpst1sg                      [<v>:] _morph_ [<prs><1sg>:м]               # Карамшоев и БД
LexiconVerbNpst2sg                      [<v>:] _morph_ [<prs><2sg>:{Й}и]            # Карамшоев и БД
LexiconVerbNpst2pl                      [<v>:] _morph_ [<prs><2pl>:{Й}ет]           # Карамшоев и БД
LexiconVerbNpst3sg                      [<v>:] _morph_ [<prs><3sg>:{vДТ}]           # Карамшоев и БД
LexiconVerbNpst3pl                      [<v>:] _morph_ [<prs><3pl>:{Й}ен]           # Карамшоев и БД
LexiconVerbNpst1sgSh                    [<v><prs><1sg>:]                            # Карамшоев и БД
LexiconVerbNpst2sgSh                    [<v><prs><2sg>:]                            # Карамшоев и БД
LexiconVerbNpst2plSh                    [<v><prs><2pl>:]                            # Карамшоев и БД
LexiconVerbNpst3sgSh                    [<v><prs><3sg>:]                            # Карамшоев и БД
LexiconVerbNpst3plSh                    [<v><prs><3pl>:]                            # Карамшоев и БД
LexiconVerbNpstF                        [<v><prs><f>:]                              # Карамшоев и БД
LexiconVerbNpstF3sg                     [<v><prs><f><2sg>:]                         # Карамшоев и БД

### Past ###
PATTERN PastVerbBase
LexiconVerbPstF                             [<v><pst><f>:]
LexiconVerbPstFPl                           [<v><pst><f_pl>:]                                               # Карамшоев и БД
LexiconVerbPstM                             [<v><pst><m>:]
LexiconVerbPstSh|LexiconVerbPstIrregular    [<v><pst>:]                                                     # Карамшоев и БД
# Past stems are equivalent to the present stem with an additional stop -t/-d (Parker 2023, p. 256)
LexiconVerbNpst     [<v>:] _morph_ [<pst>:{vДТ}]

### Imperative ###
PATTERN ImperVerbBase
LexiconVerbImper|LexiconVerbImperSh    [<v><imp>:]

### Perfect ###
PATTERN PerfVerbBase
PerfVerbBaseMasc
PerfVerbBaseOther

PATTERN PerfVerbBaseMasc
LexiconVerbPerfM            [<v><prf><m>:]
LexiconVerbNpst             [<v>:] _morph_ [<prf>:{vҶЧ}]

PATTERN PerfVerbBaseOther
LexiconVerbPerfF            [<v><prf><f>:]
LexiconVerbPerfPlRegular    [<v>:] _morph_ [<prf><pl>:{vҶЧ}]
LexiconVerbPerfPlIrregular  [<v><prf><pl>:]

### Infinitive ###
PATTERN InfVerbBase
# Infinite stems are equivalent to the present stems with an additional stop -t/-d (Parker 2023, p. 256)
LexiconVerbNpst|LexiconVerbInfShSuffixable  [<v>:] _morph_ [<inf>:{vДТ}]    (_morph_hyphen_ PurpSuff)?
LexiconVerbInfShNonsuffixable               [<v><inf>:]                     (_morph_hyphen_ PurpSuff)?

### Participle2 ###
PATTERN Participle2Base
PerfVerbBaseMasc _morph_hyphen_ [<ptcp2>:ак]

### Lexicons ###
LEXICON ToBeParadigm
видоw<v><inf>:видоw
видоw<v><inf>:вид
вад<v><pst><f>:вад
вуд<v><pst><m>:вуд
виц<v><prf><f>:виц
вуδҷ<v><prf><m>:вуδҷ
вӣм<v><prs>><1sg>:ви
вийи<v><prs>><2sg>:ви
вид<v><prs>><3sg>:ви
вийāм<v><prs>><1pl>:ви
вийет<v><prs>><2pl>:ви
вийен<v><prs>><3pl>:ви

# Parker 8.2.1
LEXICON PresSuffixes
<1sg>:{Й}ум
<2sg>:{Й}и
<3sg>:{ДТ}
<1pl>:{Й}āм 
<1pl>:{Й}ам     # internal glosses doc
<2pl>:{Й}ет
<3pl>:{Й}ен

LEXICON PurpSuff
<purp>:оw

PATTERN VerbNegation
VERBNEG _hyphen_

# Parker 8.2.2
LEXICON VERBNEG 
<neg>:на
<neg>:нā # internal gloss document
<neg>:мā

